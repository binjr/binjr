/*
 *    Copyright 2018-2026 Frederic Thevenet
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */


import org.gradle.api.internal.file.copy.CopyAction
import org.gradle.api.internal.file.copy.CopyActionProcessingStream

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.StandardCopyOption
import java.text.SimpleDateFormat
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatterBuilder
import java.time.temporal.ChronoField

plugins {
    id "de.undercouch.download" version "5.6.0"
    id "com.github.ben-manes.versions" version "0.53.0"
    id "io.github.jwharm.flatpak-gradle-generator" version "1.5.0"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0" apply true
    id "signing"
    id 'maven-publish'
}

ext.IS_TAG = System.getenv('IS_TAG') != null ? System.getenv('IS_TAG') == "true" : false
ext.IS_RELEASE = IS_TAG && !(System.getenv('REPO_TAG_NAME').endsWith('-SNAPSHOT'))
ext.IS_BETA = false
ext.BINJR_BETA_VERSION = "0.0.0"
ext.BINJR_VERSION_STEM = "3.28.0"
ext.LINUX_APP_RELEASE = "1"
ext.BINJR_VERSION = "${BINJR_VERSION_STEM}${IS_RELEASE ? '' : '-SNAPSHOT'}"
ext.BINJR_BUILD_NUMBER = System.getenv('BINJR_BUILD_NUMBER') == null ? (new DateTimeFormatterBuilder().appendPattern("YYYYMMdd.").appendValue(ChronoField.SECOND_OF_DAY).toFormatter().format(ZonedDateTime.now())) : System.getenv('BINJR_BUILD_NUMBER')
ext.GROUP_ID = 'eu.binjr'
ext.LICENSE_NAME = 'Apache License, Version 2.0'
ext.LICENSE_URL = 'http ://www.apache.org/licenses/LICENSE-2.0.txt'
ext.LICENSE_DISTRIBUTION = 'repo'
ext.BINJR_COPYRIGHT = "Copyright (c) 2016-2026 Frederic Thevenet"
ext.BINJR_DESCRIPTION = "A Time Series Browser"
ext.OS_NAME = System.getProperty("os.name").toLowerCase()
ext.TARGET_OS_NAME = isNullOrBlank(System.getenv('TARGET_OS_NAME')) ? OS_NAME : System.getenv('TARGET_OS_NAME')
ext.IS_MONOCLE = false
ext.IS_MAC = TARGET_OS_NAME.contains("mac") || TARGET_OS_NAME.contains("darwin")
ext.IS_WINDOWS = TARGET_OS_NAME.contains("windows")
ext.IS_LINUX = TARGET_OS_NAME.contains("linux")
ext.OS_FAMILY = IS_MAC ? 'mac' : IS_WINDOWS ? 'windows' : IS_LINUX ? 'linux' : 'unsupported'
ext.OS_ARCH = System.getProperty("os.arch")
ext.TARGET_ARCH = convertArchName(isNullOrBlank(System.getenv('TARGET_ARCH')) ? OS_ARCH : System.getenv('TARGET_ARCH'), "${OS_FAMILY}")
ext.IS_64 = TARGET_ARCH.toLowerCase().contains("64")
ext.BINJR_MAX_HEAP_SIZE = IS_64 ? "4096M" : "2048M"
ext.JDK_HOME = System.getProperty("java.home")
ext.OPENJFX_VERSION = "25"
ext.USE_BUILD_JDK_IN_BUNDLE = System.getenv('USE_BUILD_JDK_IN_BUNDLE') != null ? System.getenv('USE_BUILD_JDK_IN_BUNDLE') == "true" : false
ext.BUNDLE_JDK_VERSION = "jdk-25.0.1+8"
ext.BUNDLE_JDK_PATH = null
ext.OPENJFX_VERSION_STEM = OPENJFX_VERSION.replaceAll("-ea\\+[0-9]+", "")
ext.OPENJFX_PLATEFORM_CLASSIFIER = craftPlatformClassifier()
ext.DL_CACHE_PATH = System.getProperty("java.io.tmpdir") + "/gradle_dl_cache/"
ext.IS_JAVAFX_BUNDLED = ModuleLayer.boot().findModule("javafx.controls").isPresent()
ext.BUILD_DIR = layout.buildDirectory.get().toString()
ext.JFX_JMOD_PATH = System.getenv('JFX_JMOD_PATH')
ext.ADDITIONAL_JMOD_PATH = ""
if (JFX_JMOD_PATH != null){
    IS_JAVAFX_BUNDLED = true
    ADDITIONAL_JMOD_PATH = "${File.pathSeparator}${JFX_JMOD_PATH}/javafx-jmods-${OPENJFX_VERSION_STEM}"
} else {
    ADDITIONAL_JMOD_PATH = IS_JAVAFX_BUNDLED ? "" : "${File.pathSeparator}${BUILD_DIR}/jmods/javafx-jmods-${OPENJFX_VERSION_STEM}"
}
ext.JLINK_ADD_MODULES = "javafx.controls," +
        "javafx.fxml," +
        "javafx.swing," +
        "java.base," +
        "java.prefs," +
        "java.sql," +
        "java.xml," +
        "java.management," +
        "java.net.http," +
        "java.rmi," +
        "jdk.localedata," +
        "jdk.accessibility," +
        "java.management.rmi," +
        "jdk.management.agent," +
        "jdk.security.auth,jdk.management," +
        "jdk.management.jfr," +
        "jdk.zipfs," +
        "jdk.unsupported.desktop," +
        "jdk.crypto.ec," +
        "jdk.crypto.cryptoki," +
        "jdk.incubator.vector" +
        "${IS_WINDOWS ? ",jdk.crypto.mscapi" : ""}"

ext.DISTRIBUTION_NAME = "${project.name}-${BINJR_VERSION}_${OS_FAMILY}-${TARGET_ARCH}"
ext.APP_IMAGE_ROOT = "${BUILD_DIR}/app_image/${DISTRIBUTION_NAME}"
ext.APP_IMAGE_PATH = IS_WINDOWS ? "${APP_IMAGE_ROOT}" : "${APP_IMAGE_ROOT}/${BINJR_VERSION}"
ext.JPACKAGE_IMAGE_PATH = "${BUILD_DIR}/jpackage_image/${DISTRIBUTION_NAME}"
ext.FLATPAK_IMAGE_PATH = "${BUILD_DIR}/flatpak/"
ext.WIX_HOME = System.getenv("WIX_HOME") != null ? System.getenv("WIX_HOME") : System.getenv("WIX6")
ext.ARTIFACTS_ROOT_PATH = "${BUILD_DIR}/artifacts/"
ext.ARTIFACTS_PATH_ZIP = file("${ARTIFACTS_ROOT_PATH}/zip/")
ext.ARTIFACTS_PATH_MSI = file("${ARTIFACTS_ROOT_PATH}/msi/")
ext.ARTIFACTS_PATH_DMG = file("${ARTIFACTS_ROOT_PATH}/dmg/")
ext.ARTIFACTS_PATH_PKG = file("${ARTIFACTS_ROOT_PATH}/pkg/")
ext.ARTIFACTS_PATH_RPM = file("${ARTIFACTS_ROOT_PATH}/rpm/")
ext.ARTIFACTS_PATH_DEB = file("${ARTIFACTS_ROOT_PATH}/deb/")
ext.ARTIFACTS_PATH_TGZ = file("${ARTIFACTS_ROOT_PATH}/tgz/")
ext.ARTIFACTS_PATH_DEP = file("${ARTIFACTS_ROOT_PATH}/dep/")

ext.DEP_CACHE_NAME = "build-dependencies-${BINJR_VERSION}_${OS_FAMILY}-${TARGET_ARCH}.zip"

ext."signing.keyId" = System.getenv('GPG_KEY_NAME')
ext."signing.secretKeyRingFile" = System.getenv('GPG_KEYRING_PATH')
ext."signing.password" = System.getenv('GPG_PASSPHRASE')

ext.ROOT_DIR = "${projectDir}"

System.out.println("Building version: ${BINJR_VERSION}")
System.out.println("Is tag: ${IS_TAG}")
System.out.println("Is release: ${IS_RELEASE}")

if (IS_TAG && !System.getenv('REPO_TAG_NAME').endsWith("v${BINJR_VERSION}")) {
    throw new GradleException("Tag name [${System.getenv('REPO_TAG_NAME')}] does not match version [${BINJR_VERSION}]")
}

configure(subprojects) {
    if (project == project(':binjr-app')) {
        apply plugin: 'application'
        apply plugin: 'io.github.jwharm.flatpak-gradle-generator'

        tasks.flatpakGradleGenerator {
            outputFile = file("${BUILD_DIR}/${project.name}-sources.json")
            downloadDirectory = "./offline-repository"
            excludeConfigurations = ["testCompileClasspath", "testRuntimeClasspath"]
        }
    } else {
        apply plugin: 'java-library'
    }
    apply plugin: "maven-publish"
    apply plugin: "signing"
    group = GROUP_ID
    version = BINJR_VERSION
    description = BINJR_DESCRIPTION
    compileJava.options.encoding = 'UTF-8'

    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    if (project != project(':binjr-app')) {
        project.javadoc {
            options.encoding = 'UTF-8'
            options.tags = ["XmlJavaTypeAdapter", "XmlJavaTypeAdapters"]
        }

        tasks.register('javadocJar', Jar) {
            archiveClassifier = 'javadoc'
            from javadoc
        }

        tasks.register('sourcesJar', Jar) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        dependencies {
            testImplementation('org.junit.jupiter:junit-jupiter-api:6.0.1')
            testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:6.0.1')
            compileOnly "org.openjfx:javafx-base:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-graphics:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-controls:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-fxml:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-swing:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Central Portal Snapshots'
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    maven { url "${ROOT_DIR}/offline-repository" }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                if (project != project(':binjr-app')) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
                pom {
                    name = project.name
                    description = 'A Time Series Data Browser'
                    url = 'https://binjr.eu'
                    if (project == project(':binjr-app')) {
                        packaging = 'pom'
                    } else {
                        packaging = 'jar'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'fthevenet'
                            name = 'Frederic Thevenet'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/binjr/binjr.git'
                        developerConnection = 'scm:git:https://github.com/binjr/binjr.git'
                        url = 'https://github.com/binjr/binjr'
                    }
                    if (project == project(':binjr-app')) {
                        withXml {
                            Node pluginNode = asNode().appendNode('build')
                                    .appendNode('plugins')
                                    .appendNode('plugin')
                            pluginNode.appendNode('groupId', 'org.codehaus.mojo')
                            pluginNode.appendNode('artifactId', 'exec-maven-plugin')
                            pluginNode.appendNode('version', '1.2.1')
                            pluginNode.appendNode('executions')
                                    .appendNode('execution')
                                    .appendNode('goals')
                                    .appendNode('goal', 'java')
                            pluginNode.appendNode('configuration')
                                    .appendNode('mainClass', 'eu.binjr.core.Bootstrap')

                            Node depsNode = asNode().appendNode('dependencies')
                            project.configurations.runtimeClasspath.each {
                                if (it.name.startsWith('binjr')) {
                                    Node depNode = depsNode.appendNode('dependency')
                                    depNode.appendNode('groupId', GROUP_ID)
                                    depNode.appendNode('artifactId', it.name.replace("-${BINJR_VERSION}.jar", ""))
                                    depNode.appendNode('version', BINJR_VERSION)
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    signing {
        required = { IS_RELEASE && gradle.taskGraph.hasTask("publishArtifacts") }
        sign publishing.publications.mavenJava
    }

    tasks.withType(Sign) {
        onlyIf { IS_RELEASE }
    }
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            stagingProfileId = "binjr"
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

tasks.flatpakGradleGenerator {
    outputFile = file("${BUILD_DIR}/binjr-plugins-sources.json")
    downloadDirectory = "./offline-repository"
    excludeConfigurations = ["testCompileClasspath", "testRuntimeClasspath"]
}


tasks.register('publishArtifacts') {
    dependsOn publishToSonatype
    if (IS_RELEASE) {
        finalizedBy(closeSonatypeStagingRepository)
    }
    doLast {
        System.out.println("Done publishing artifacts from subprojects.")
    }
}

tasks.register('copyAppLibs', Copy) {
    into "$APP_IMAGE_PATH/libs"
    from(project(":binjr-core").configurations.runtimeClasspath) {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jrds").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-csv").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jfr").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
//    from project(":binjr-adapter-json").configurations.runtimeClasspath {
//        exclude "javafx-*.jar"
//    }
    from project(":binjr-adapter-jvmgc").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
        exclude "binjr-adapter-logs-*.jar"
    }
    from project(":binjr-adapter-rrd4j").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-netdata").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
//    from project(":binjr-adapter-text").configurations.runtimeClasspath {
//        exclude "javafx-*.jar"
//    }
    from project(":binjr-adapter-logs").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-core").jar
}

tasks.register('copyPluginLibs', Copy) {
    into IS_MAC ? "$APP_IMAGE_PATH/libs" : "$APP_IMAGE_PATH/plugins"
    from project(":binjr-adapter-jrds").jar
    from project(":binjr-adapter-csv").jar
    from project(":binjr-adapter-jfr").jar
//    from project(":binjr-adapter-json").jar
    from project(":binjr-adapter-jvmgc").jar
    from project(":binjr-adapter-rrd4j").jar
    from project(":binjr-adapter-netdata").jar
//    from project(":binjr-adapter-text").jar
    from project(":binjr-adapter-logs").jar
}

tasks.register('copyJpackageInputLibs', Copy) {
    into "${BUILD_DIR}/tmp/input"
    from(project(":binjr-core").configurations.runtimeClasspath) {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jrds").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jfr").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jvmgc").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-csv").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
//    from project(":binjr-adapter-json").configurations.runtimeClasspath {
//        exclude "javafx-*.jar"
//    }
    from project(":binjr-adapter-rrd4j").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-netdata").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
//    from project(":binjr-adapter-text").configurations.runtimeClasspath {
//        exclude "javafx-*.jar"
//    }
    from project(":binjr-adapter-logs").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-core").jar
    from project(":binjr-adapter-jrds").jar
    from project(":binjr-adapter-jfr").jar
    from project(":binjr-adapter-jvmgc").jar
    from project(":binjr-adapter-csv").jar
//    from project(":binjr-adapter-json").jar
    from project(":binjr-adapter-rrd4j").jar
    from project(":binjr-adapter-netdata").jar
//    from project(":binjr-adapter-text").jar
    from project(":binjr-adapter-logs").jar
}

tasks.register('copyPlatformSpecific', Copy) {
    into APP_IMAGE_PATH
    from "${projectDir}/distribution/platforms/${OS_FAMILY}"
}

tasks.register('copyResources', Copy) {
    into "$APP_IMAGE_PATH/resources"
    from "${projectDir}/distribution/resources"
}

tasks.register('expandMdTemplates', Copy) {
    from "${projectDir}/distribution/templates"
    into "${BUILD_DIR}/tmp/expanded"
    filteringCharset = 'UTF-8'
    def changeHistory = new File("${project.projectDir}/CHANGELOG.md").getText("UTF-8")
    def currentChanges = new File("${project.projectDir}/UNRELEASED.md").getText("UTF-8")
    expand(["version"       : "$BINJR_VERSION",
            "releaseDate"   : "${new SimpleDateFormat("EEE, d MMM yyyy").format(new Date())}",
            "releaseDateIso": "${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}",
            "changeHistory" : "$changeHistory",
            "currentChanges": "$currentChanges",
            "tagName"       : "v${BINJR_VERSION}",
            "copyright"     : "${BINJR_COPYRIGHT}",
            "isDraft"       : "${IS_RELEASE ? "false" : "true"}"
    ])
}

tasks.register('copyInfo', Copy) {
    dependsOn expandMdTemplates
    from {
        ["${BUILD_DIR}/tmp/expanded/CHANGELOG",
         "${BUILD_DIR}/tmp/expanded/LICENSE",
         "${BUILD_DIR}/tmp/expanded/NOTICE",
         "${BUILD_DIR}/tmp/expanded/README"]
    }
    into "${APP_IMAGE_PATH}"
}

tasks.register('downloadOpenJfxModules', Download) {
    onlyIfModified true
    src craftOpenJfxModsUrl()
    dest new File(DL_CACHE_PATH, "openjfx-jmods_${OPENJFX_VERSION}.zip")
}

tasks.register('downloadJlinkOpenJdk', Download) {
    onlyIfModified true
    src craftJlinkJmodUrl()
    dest new File(DL_CACHE_PATH, "openjdk_${BUNDLE_JDK_VERSION}-jmods${IS_WINDOWS ? '.zip' : '.tar.gz'}")
}

tasks.register('unzipOpenJfxModules', Copy) {
    dependsOn downloadOpenJfxModules
    from zipTree(downloadOpenJfxModules.dest)
    into "${BUILD_DIR}/jmods/"
}

tasks.register('unzipJlinkOpenJdk', Copy) {
    dependsOn downloadJlinkOpenJdk
    from zipTree(downloadJlinkOpenJdk.dest)
    into "${BUILD_DIR}/jlinkOpenJDK/"
}

tasks.register('untarJlinkOpenJdk', Copy) {
    dependsOn downloadJlinkOpenJdk
    from tarTree(downloadJlinkOpenJdk.dest)
    into "${BUILD_DIR}/jlinkOpenJDK/"
}

tasks.register('createRuntimeImage', Exec) {
    if (!IS_JAVAFX_BUNDLED) {
        dependsOn unzipOpenJfxModules
    }
    def platformJlinkDir = JDK_HOME
    def targetJlinkDir
    if (USE_BUILD_JDK_IN_BUNDLE) {
        targetJlinkDir = platformJlinkDir
    } else if (Files.exists(Paths.get("${BUNDLE_JDK_PATH}/jmods/java.base.jmod"))) {
        targetJlinkDir = BUNDLE_JDK_PATH
    } else {
        if (IS_WINDOWS) {
            dependsOn unzipJlinkOpenJdk
        } else {
            dependsOn untarJlinkOpenJdk
        }
        targetJlinkDir = "${BUILD_DIR}/jlinkOpenJDK/${BUNDLE_JDK_VERSION}-jmods".replace("-ea-beta", "")
    }


    workingDir project.projectDir
    commandLine = [
            "${platformJlinkDir}/bin/jlink",
            '-p', "${targetJlinkDir}${ADDITIONAL_JMOD_PATH}",
            '--add-modules', JLINK_ADD_MODULES,
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            "--vm=server",
            "--verbose",
            "--compress=zip-6",
            '--output', "${APP_IMAGE_PATH}/runtime"
    ]
    doLast {
        System.out.println("Application '${project.name}' packaged.")
        System.out.println(" -> location: ${APP_IMAGE_PATH}/")
    }
}

tasks.register('prepareAppBundle') {
    dependsOn copyAppLibs, copyPluginLibs, copyResources, copyPlatformSpecific, copyInfo, createRuntimeImage
    doFirst {
        mkdir ARTIFACTS_ROOT_PATH
    }
}

tasks.register('listPackageContent', Exec) {
    dependsOn prepareAppBundle
    workingDir APP_IMAGE_PATH
    commandLine = [
            "sh", "-c",
            "find . -mindepth 1 | sed -n 's@^\\./@@p' > .installed"
    ]
}

tasks.register('packageDistributionZip', Zip) {
    dependsOn prepareAppBundle
    doFirst {
        Files.copy(Paths.get("${projectDir}/distribution/bundlers/win_zip/binjr.ini"),
                Paths.get("${APP_IMAGE_PATH}/binjr.ini"),
                StandardCopyOption.REPLACE_EXISTING)
    }
    from APP_IMAGE_ROOT
    destinationDirectory = ARTIFACTS_PATH_ZIP
    archiveFileName = "${DISTRIBUTION_NAME}.zip"
    outputs.dir ARTIFACTS_PATH_ZIP
}

tasks.register('compressJpackageAsTar', Tar) {
    dependsOn jpackageAppMac
    setCompression(Compression.GZIP)
    from JPACKAGE_IMAGE_PATH
    destinationDirectory = ARTIFACTS_PATH_TGZ
    archiveFileName = "${DISTRIBUTION_NAME}.tar.gz"
    outputs.dir ARTIFACTS_PATH_TGZ
}

tasks.register('packageDistributionTar', Exec) {
    dependsOn listPackageContent
    workingDir APP_IMAGE_ROOT
    commandLine = [
            "sh", "-c",
            "ln -s \"${BINJR_VERSION}/binjr\" \"binjr\" && tar czf \"${ARTIFACTS_PATH_TGZ}/${DISTRIBUTION_NAME}.tar.gz\" \"${BINJR_VERSION}\" \"binjr\""
    ]
    outputs.dir ARTIFACTS_PATH_TGZ
}

tasks.register('wixAddUiExt', Exec) {
    dependsOn prepareAppBundle
    commandLine = ["${WIX_HOME}wix.exe", "extension", "add", "-g", "WixToolset.UI.wixext"]
}

tasks.register('wixAddUtilExt', Exec) {
    dependsOn wixAddUiExt
    commandLine = ["${WIX_HOME}wix.exe", "extension", "add", "-g", "WixToolset.Util.wixext"]
}

tasks.register('wixRunBuild', Exec) {
    dependsOn wixAddUtilExt
    doFirst {
        Files.copy(Paths.get("${projectDir}/distribution/bundlers/win_msi/binjr.ini"),
                Paths.get("${APP_IMAGE_PATH}/binjr.ini"),
                StandardCopyOption.REPLACE_EXISTING)
    }

    workingDir project.projectDir
    commandLine = [
            "${WIX_HOME}wix.exe", "build", "-arch", "${getArch()}",
            "${projectDir}/distribution/bundlers/win_msi/binjr.wxs",
            "-ext", "WixToolset.UI.wixext",
            "-ext", "WixToolset.Util.wixext",
            "-d", "binjrVersion=${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}",
            "-d", "buildSourceDir=${APP_IMAGE_PATH}",
            "-d", "resourcesDir=${projectDir}/distribution",
            "-out", "${ARTIFACTS_PATH_MSI}/${DISTRIBUTION_NAME}.msi",
            "-pdbtype", "none"
    ]
    outputs.dir ARTIFACTS_PATH_MSI
}

tasks.register('jpackageAppWin', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir JPACKAGE_IMAGE_PATH
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "app-image",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", "${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}",
            "--icon", "${projectDir}/distribution/platforms/windows/resources/icons/binjr.ico",
            "--arguments", "--packaging=WIN_ZIP",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED"
    ]
    outputs.dir JPACKAGE_IMAGE_PATH
}

tasks.register('jpackageAppMac', Exec) {
    dependsOn createRuntimeImage, copyJpackageInputLibs
    workingDir JPACKAGE_IMAGE_PATH
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "app-image",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", "${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}",
            "--icon", "${projectDir}/distribution/platforms/mac/resources/icons/binjr.icns",
            "--arguments", "--packaging=MAC_TAR",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--java-options", "-Dapple.awt.application.appearance=system"
    ]
    outputs.dir JPACKAGE_IMAGE_PATH
}

tasks.register('jpackageLinuxFlatpak', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir FLATPAK_IMAGE_PATH
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "app-image",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", BINJR_VERSION.replace("-", "."),
            "--icon", "${projectDir}/distribution/platforms/linux/resources/icons/binjr.png",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--arguments", "--packaging=LINUX_FPK"
    ]
    outputs.dir FLATPAK_IMAGE_PATH
}

tasks.register('jpackageDmg', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir ARTIFACTS_PATH_DMG
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "dmg",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", "${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}",
            "--icon", "${APP_IMAGE_PATH}/resources/icons/binjr.icns",
            "--file-associations", "${projectDir}/distribution/bundlers/mac_dmg/file-associations.properties",
            "--arguments", "--packaging=MAC_DMG",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--java-options", "-Dapple.awt.application.appearance=system"
    ]
    doLast {
        def source = Path.of("${ARTIFACTS_PATH_DMG}/${project.name}-${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}.dmg")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.dmg"))
    }
    outputs.dir ARTIFACTS_PATH_DMG
}

tasks.register('jpackagePkg', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir ARTIFACTS_PATH_PKG
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "pkg",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", "${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}",
            "--icon", "${APP_IMAGE_PATH}/resources/icons/binjr.icns",
            "--file-associations", "${projectDir}/distribution/bundlers/mac_pkg/file-associations.properties",
            "--arguments", "--packaging=MAC_PKG",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--java-options", "-Dapple.awt.application.appearance=system"
    ]
    doLast {
        def source = Path.of("${ARTIFACTS_PATH_PKG}/${project.name}-${IS_BETA ? BINJR_BETA_VERSION : BINJR_VERSION_STEM}.pkg")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.pkg"))
    }
    outputs.dir ARTIFACTS_PATH_PKG
}

tasks.register('jpackageDeb', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir ARTIFACTS_PATH_DEB
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "deb",
            "--copyright", BINJR_COPYRIGHT,
            "--description", BINJR_DESCRIPTION,
            "--app-version", BINJR_VERSION,
            "--icon", "${APP_IMAGE_PATH}/resources/icons/binjr.png",
            "--license-file", "${projectDir}/LICENSE.md",
            "--file-associations", "${projectDir}/distribution/bundlers/linux_deb/file-associations.properties",
            "--resource-dir", "${projectDir}/distribution/bundlers/linux_deb/",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--arguments", "--system-plugins-path=/opt/binjr/plugins",
            "--arguments", "--packaging=LINUX_DEB",
            "--linux-package-name", "binjr",
            "--linux-deb-maintainer", "binjr@free.fr",
            "--linux-app-category", "Utility",
            "--linux-app-release", LINUX_APP_RELEASE,
            "--linux-menu-group", "Utility",
            "--linux-rpm-license-type", "Apache-2.0",
            "--linux-shortcut",
            "--temp", "${BUILD_DIR}/tmp/apt",
            "--verbose"
    ]
    doLast {
        def source = Path.of("${ARTIFACTS_PATH_DEB}/${makeDebPackageName()}")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.deb"))
    }
    outputs.dir ARTIFACTS_PATH_DEB
}

tasks.register('jpackageRpm', Exec) {
    dependsOn prepareAppBundle, copyJpackageInputLibs
    workingDir ARTIFACTS_PATH_RPM
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${BUILD_DIR}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Bootstrap",
            "--runtime-image", "${APP_IMAGE_PATH}/runtime",
            "--type", "rpm",
            "--copyright", "${BINJR_COPYRIGHT}",
            "--description", "${BINJR_DESCRIPTION}",
            "--app-version", "${BINJR_VERSION.replace("-", ".")}",
            "--icon", "${APP_IMAGE_PATH}/resources/icons/binjr.png",
            "--license-file", "${projectDir}/LICENSE.md",
            "--file-associations", "${projectDir}/distribution/bundlers/linux_rpm/file-associations.properties",
            "--java-options", "-Xmx${BINJR_MAX_HEAP_SIZE}",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--java-options", "-XX:+UseCompactObjectHeaders",
            "--java-options", "--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED",
            "--java-options", "--add-exports=javafx.controls/com.sun.javafx.charts=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.geometry=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/com.sun.javafx.scene.traversal=ALL-UNNAMED",
            "--java-options", "--add-opens=javafx.graphics/javafx.scene.paint=ALL-UNNAMED",
            "--java-options", "--enable-native-access=javafx.graphics,ALL-UNNAMED",
            "--java-options", "--add-modules=jdk.incubator.vector",
            "--arguments", "--system-plugins-path=/opt/binjr/plugins",
            "--arguments", "--packaging=LINUX_RPM",
            "--linux-package-name", "binjr",
            "--linux-app-category", "Utility",
            "--linux-app-release", LINUX_APP_RELEASE,
            "--linux-menu-group", "Utility;Utilities;Development;Profiling;",
            "--linux-rpm-license-type", "Apache-2.0",
            "--linux-shortcut",
            "--temp", "${BUILD_DIR}/tmp/rpm",
            "--verbose"
    ]
    doLast {
        def source = Path.of("${ARTIFACTS_PATH_RPM}/${makeRpmPackageName()}")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.rpm"))
    }
    outputs.dir ARTIFACTS_PATH_RPM
}

tasks.register('packageAsZip', WrapTaskForSigning) {
    dependsOn packageDistributionZip
    from "${ARTIFACTS_PATH_ZIP}/${DISTRIBUTION_NAME}.zip"
    destinationDirectory = ARTIFACTS_PATH_ZIP
    archiveFileName = "${DISTRIBUTION_NAME}.zip"
}

tasks.register('packageAsMsi', WrapTaskForSigning) {
    dependsOn wixRunBuild
    from "${ARTIFACTS_PATH_MSI}/${DISTRIBUTION_NAME}.msi"
    destinationDirectory = ARTIFACTS_PATH_MSI
    archiveFileName = "${DISTRIBUTION_NAME}.msi"
}

tasks.register('packageAsDmg', WrapTaskForSigning) {
    dependsOn jpackageDmg
    from "${ARTIFACTS_PATH_DMG}/${DISTRIBUTION_NAME}.dmg"
    destinationDirectory = ARTIFACTS_PATH_DMG
    archiveFileName = "${DISTRIBUTION_NAME}.dmg"
}

tasks.register('packageAsPkg', WrapTaskForSigning) {
    dependsOn jpackagePkg
    from "${ARTIFACTS_PATH_PKG}/${DISTRIBUTION_NAME}.pkg"
    destinationDirectory = ARTIFACTS_PATH_PKG
    archiveFileName = "${DISTRIBUTION_NAME}.pkg"
}

tasks.register('packageAsTarGz', WrapTaskForSigning) {
    dependsOn packageDistributionTar
    from "${ARTIFACTS_PATH_TGZ}/${DISTRIBUTION_NAME}.tar.gz"
    destinationDirectory = ARTIFACTS_PATH_TGZ
    archiveFileName = "${DISTRIBUTION_NAME}.tar.gz"
}

tasks.register('packageAsMacApp', WrapTaskForSigning) {
    dependsOn compressJpackageAsTar
    from "${ARTIFACTS_PATH_TGZ}/${DISTRIBUTION_NAME}.tar.gz"
    destinationDirectory = ARTIFACTS_PATH_TGZ
    archiveFileName = "${DISTRIBUTION_NAME}.tar.gz"
}

tasks.register('packageAsRpm', WrapTaskForSigning) {
    dependsOn jpackageRpm
    from "${ARTIFACTS_PATH_RPM}/${DISTRIBUTION_NAME}.rpm"
    destinationDirectory = ARTIFACTS_PATH_RPM
    archiveFileName = "${DISTRIBUTION_NAME}.rpm"
}

tasks.register('packageAsDeb', WrapTaskForSigning) {
    dependsOn jpackageDeb
    from "${ARTIFACTS_PATH_DEB}/${DISTRIBUTION_NAME}.deb"
    destinationDirectory = ARTIFACTS_PATH_DEB
    archiveFileName = "${DISTRIBUTION_NAME}.deb"
}

signing {
    sign packageAsZip
    sign packageAsMsi
    sign packageAsPkg
    sign packageAsTarGz
    sign packageAsRpm
    sign packageAsDeb
    sign packageAsMacApp
}

tasks.register('packageDistribution') {
    if (IS_WINDOWS) {
        finalizedBy(packageAsMsi, packageAsZip)
    } else if (IS_MAC) {
        finalizedBy(packageAsPkg, packageAsMacApp)
    } else {
        finalizedBy(packageAsTarGz, packageAsDeb, packageAsRpm)
    }
}

tasks.register('signPackageDistribution') {
    if (IS_WINDOWS) {
        finalizedBy(signPackageAsMsi, signPackageAsZip)
    } else if (IS_MAC) {
        finalizedBy(signPackageAsPkg, signPackageAsMacApp)
    } else {
        finalizedBy(signPackageAsTarGz, signPackageAsDeb, signPackageAsRpm)
    }
}


def isNonStable = { String version ->
    // Ignore javafx early access and glassfish beta
    def regex = /(?i)^[0-9,.v-]+\-((ea)|(rc)|(alpha)|(beta)|(b)|(m))[-\+]*[0-9.]+b?$/
    return (version ==~ regex)
}


def craftPlatformClassifier() {
    def classifier
    if (IS_MAC) {
        classifier = "mac"
        if (TARGET_ARCH == "aarch64") {
            classifier += "-aarch64"
        }
    } else if (IS_LINUX) {
        classifier = "linux"
        if (TARGET_ARCH == "aarch64") {
            classifier += "-aarch64"
        } else if (TARGET_ARCH == "aarch32" || TARGET_ARCH == "arm32") {
            classifier += "-arm32"
        }
    } else if (IS_WINDOWS) {
        classifier = "win"
        if (TARGET_ARCH == "x86") {
            classifier += "-x86"
        }
    }
    if (IS_MONOCLE) {
        classifier += "-monocle"
    }
    return classifier
}

def craftJlinkJmodUrl() {
    def platform
    switch (OS_FAMILY) {
        case "windows":
            platform = "windows"
            break
        case "linux":
            platform = "linux"
            break
        case "mac":
            platform = "mac"
            break
        default:
            throw new Exception("Cannot download OpenJDK modules for unsupported platform: ${OS_FAMILY} - ${TARGET_ARCH}")
    }
    def arch = getArch()
    return "https://api.adoptium.net/v3/binary/version/${BUNDLE_JDK_VERSION}/${platform}/${arch}/jmods/hotspot/normal/adoptium"
}

def craftOpenJfxModsUrl() {
    def platform
    switch (OS_FAMILY) {
        case "windows":
            platform = "windows"
            break
        case "linux":
            platform = "linux"
            break
        case "mac":
            platform = "osx"
            break
        default:
            throw new Exception("Cannot download OpenJFX modules for unsupported platform: ${OS_FAMILY} - ${TARGET_ARCH}")
    }
    def arch = getArch()
    return "https://download2.gluonhq.com/openjfx/${OPENJFX_VERSION_STEM}/openjfx-${OPENJFX_VERSION}_${platform}-${arch}_bin-jmods.zip"
}

static def convertArchName(String archName, String packager) {
    switch (archName) {
        case "x64":
        case "amd64":
        case "x86_64":
            switch (packager) {
                case "mac":
                case "windows":
                case "linux":
                case "rpm":
                    return "x86_64"
                case "deb":
                case "apt":
                    return "amd64"
            }
            break
        case "arm64":
        case "aarch64":
            switch (packager) {
                case "mac":
                case "windows":
                case "linux":
                case "rpm":
                    return "aarch64"
                case "deb":
                case "apt":
                    return "arm64"
            }
    }
    return archName
}


def makeDebPackageName() {
    return "${project.name}_${BINJR_VERSION}-${LINUX_APP_RELEASE}_${convertArchName("${TARGET_ARCH}", "deb")}.deb"
}

def makeRpmPackageName() {
    return "${project.name}-${BINJR_VERSION.replace("-", ".")}-${LINUX_APP_RELEASE}.${convertArchName("${TARGET_ARCH}", "rpm")}.rpm"
}

def getArch() {
    def arch
    switch (TARGET_ARCH) {
        case "x64":
        case "amd64":
        case "x86_64":
            arch = "x64"
            break
        case "x86":
            arch = "x86"
            break
        case "aarch64":
        case "arm64":
            arch = "aarch64"
            break
        default:
            throw new Exception("Cannot download OpenJFX modules for unsupported platform: ${OS_FAMILY} - ${TARGET_ARCH}")
    }
    return arch
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

static boolean isNullOrBlank(String val) {
    return val == null || val.isBlank();
}


/**
 * This class is a no-op copy task whose purpose is to allow using the
 * standard gradle signing plugin to sign any arbitrary file.
 * The idea is to piggy-back on the existing support for signing
 * of tasks extending AbstractArchiveTask; to use it, simply declare a
 * task of that type and use the path to the file to be signed both as
 * the source and target of the archive.
 * NB: No copy/move/archiving will actually take place.
 */

abstract class WrapTaskForSigning extends AbstractArchiveTask {
    @Override
    protected CopyAction createCopyAction() {
        return new CopyAction() {
            @Override
            WorkResult execute(CopyActionProcessingStream stream) {
                return WorkResults.didWork(true);
            }
        }
    }
}
