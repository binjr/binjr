/*
 *    Copyright 2017-2018 Frederic Thevenet
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id "de.undercouch.download" version "3.4.3"
    id "org.kordamp.markdown.convert" version "1.2.0"
    id 'io.codearte.nexus-staging' version '0.12.0'
}

ext.IS_RELEASE = System.getenv('IS_TAG') != null ? (System.getenv('IS_TAG') == "true") : false

ext.BINJR_VERSION = "2.0.0${IS_RELEASE ? '' : '-SNAPSHOT'}"

if (IS_RELEASE && System.getenv('REPO_TAG_NAME') != "v${BINJR_VERSION}") {
    throw new GradleException("Tag name [${System.getenv('REPO_TAG_NAME')}] does not match version [${BINJR_VERSION}]")
}

ext.BINJR_BUILD_NUMBER = System.getenv('BINJR_BUILD_NUMBER') == null ? ("000") : System.getenv('BINJR_BUILD_NUMBER')

ext.LICENSE_NAME = 'Apache License, Version 2.0'
ext.LICENSE_URL = 'http ://www.apache.org/licenses/LICENSE-2.0.txt'
ext.LICENSE_DISTRIBUTION = 'repo'

ext.OS_NAME = System.getProperty("os.name").toLowerCase()
ext.OS_ARCH = System.getProperty("os.arch")
ext.IS_64 = OS_ARCH.toLowerCase().contains("64")
ext.IS_MAC = OS_NAME.contains("mac") || OS_NAME.contains("darwin")
ext.IS_WINDOWS = OS_NAME.contains("windows")
ext.IS_LINUX = OS_NAME.contains("linux")
ext.OS_FAMILY = IS_MAC ? 'mac' : IS_WINDOWS ? 'windows' : IS_LINUX ? 'linux' : 'unsupported'
ext.JDK_HOME = System.getProperty("java.home")
ext.OPENJFX_VERSION = "11.0.1"
ext.OPENJFX_PLATEFORM_CLASSIFIER = IS_MAC ? 'mac' : IS_WINDOWS ? 'win' : IS_LINUX ? 'linux' : 'unsupported'
ext.OPENJFX_JMODS_DOWNLOAD_URL = "https://gluonhq.com/download/javafx-${OPENJFX_VERSION.replace(".", "-")}-jmods-${OS_FAMILY}"
ext.JLINK_ADD_MODULES = "javafx.controls,javafx.fxml,javafx.swing,java.base,java.prefs,java.sql,java.xml,java.management,java.rmi," +
        "java.management.rmi,jdk.management.agent,jdk.security.auth,jdk.management,jdk.management.jfr,jdk.zipfs," +
        "jdk.unsupported.desktop${IS_WINDOWS ? ",jdk.crypto.mscapi" : ""}"
ext.DISTRIBUTION_NAME = "${project.name}-${BINJR_VERSION}_${OS_FAMILY}-${OS_ARCH}"
ext.DISTRIBUTION_PATH = "${buildDir}/distributions/${DISTRIBUTION_NAME}"
ext.ARCHIVE_PATH = ""

ext."signing.keyId" = System.getenv('GPG_KEY_NAME')
ext."signing.secretKeyRingFile" = "${System.getProperty("user.home")}/.gnupg/secring.gpg"
ext."signing.password" = System.getenv('GPG_PASSPHRASE')

configure(subprojects - project(':binjr-app')) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group 'eu.fthevenet'
    version BINJR_VERSION
    sourceCompatibility = 11
    description 'Flexible Time Series Visualization'
    compileJava.options.encoding = 'UTF-8'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    dependencies {
        compileOnly "org.openjfx:javafx-base:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        compileOnly "org.openjfx:javafx-graphics:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        compileOnly "org.openjfx:javafx-controls:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        compileOnly "org.openjfx:javafx-fxml:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        compileOnly "org.openjfx:javafx-swing:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
    }

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = 'Flexible Time Series Visualization'
                    url = 'https://www.binjr.eu'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'fthevenet'
                            name = 'Frederic Thevenet'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/fthevenet/binjr.git'
                        developerConnection = 'scm:git:https://github.com/fthevenet/binjr.git'
                        url = 'https://github.com/fthevenet/binjr'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getenv('OSSRH_JIRA_USERNAME')
                    password System.getenv('OSSRH_JIRA_PASSWORD')
                }
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = IS_RELEASE ? releasesRepoUrl : snapshotsRepoUrl
            }
        }
        doLast{
            if (IS_RELEASE) {
                closeAndReleaseRepository
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}

task clean(type: Delete) {
    delete buildDir
}

task copyAppLibs(type: Copy) {
    into "$DISTRIBUTION_PATH/libs"
    from project(":binjr-core").configurations.runtimeClasspath
    from project(":binjr-core").jar
}

task copyPluginLibs(type: Copy) {
    into "$DISTRIBUTION_PATH/plugins"
    from project(":binjr-adapter-jrds").jar
    from project(":binjr-adapter-csv").jar
}

task copyLauncher(type: Copy) {
    into DISTRIBUTION_PATH
    from "${projectDir}/distribution/platforms/${OS_FAMILY}"
}

task copyResources(type: Copy) {
    into "$DISTRIBUTION_PATH/resources"
    from "${projectDir}/distribution/resources"
}

markdownToHtml {
    sourceDir = file("${projectDir}/distribution/info")
    outputDir = file("$DISTRIBUTION_PATH")
}

task downloadopenJfxJmods(type: Download) {
    src OPENJFX_JMODS_DOWNLOAD_URL
    dest new File("${buildDir}/tmp", 'openjfx-jmods.zip')
}

task downloadAndUnzipFile(dependsOn: downloadopenJfxJmods, type: Copy) {
    from zipTree(downloadopenJfxJmods.dest)
    into buildDir
}

task createRuntimeImage(type: Exec, dependsOn: [downloadAndUnzipFile]) {
    workingDir project.projectDir
    commandLine = [
            'jlink',
            '-p', "${JDK_HOME}/jmods${File.pathSeparator}${buildDir}/javafx-jmods-${OPENJFX_VERSION}",
            '--add-modules', JLINK_ADD_MODULES,
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            "--vm=server",
            "--compress=2",
            '--output', "${DISTRIBUTION_PATH}/runtime"
    ]
    doLast {
        System.out.println("Application '${project.name}' packaged.")
        System.out.println(" -> location: ${DISTRIBUTION_PATH}/")
    }
}

nexusStaging {
    packageGroup = 'eu.fthevenet'
}

task packageDistributionZip(type: Zip) {
    from DISTRIBUTION_PATH
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.zip"
}

task packageDistributionTar(type: Tar) {
    compression Compression.GZIP
    from DISTRIBUTION_PATH
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.tar.gz"
}

task packageDistribution(dependsOn: [copyAppLibs, copyPluginLibs, copyResources, copyLauncher, markdownToHtml, createRuntimeImage]) {
    if (IS_WINDOWS) {
        (packageDistributionZip)
        ARCHIVE_PATH = "$buildDir/${packageDistributionZip.archiveName}"
    } else {
        finalizedBy(packageDistributionTar)
        ARCHIVE_PATH = "$buildDir/${packageDistributionTar.archiveName}"
    }
}

