/*
 *    Copyright 2018-2020 Frederic Thevenet
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */


import org.gradle.api.internal.file.copy.CopyAction
import org.gradle.api.internal.file.copy.CopyActionProcessingStream

import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption
import java.text.SimpleDateFormat
import java.time.Duration

plugins {
    id "de.undercouch.download" version "4.0.4"
    id "org.kordamp.markdown.convert" version "1.2.0"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "de.marcphilipp.nexus-publish" version "0.4.0" apply false
    id "signing"
}

ext.IS_TAG = System.getenv('IS_TAG') != null ? System.getenv('IS_TAG') == "true" : false
ext.IS_RELEASE = IS_TAG && !(System.getenv('REPO_TAG_NAME').endsWith('-SNAPSHOT'))
ext.BINJR_VERSION_STEM = "2.17.1"
ext.BINJR_VERSION = "${BINJR_VERSION_STEM}${IS_RELEASE ? '' : '-SNAPSHOT'}"
ext.BINJR_BUILD_NUMBER = System.getenv('BINJR_BUILD_NUMBER') == null ? ("000") : System.getenv('BINJR_BUILD_NUMBER')
ext.GROUP_ID = 'eu.binjr'
ext.LICENSE_NAME = 'Apache License, Version 2.0'
ext.LICENSE_URL = 'http ://www.apache.org/licenses/LICENSE-2.0.txt'
ext.LICENSE_DISTRIBUTION = 'repo'
ext.OS_NAME = System.getProperty("os.name").toLowerCase()
ext.OS_ARCH = System.getProperty("os.arch")
ext.IS_64 = OS_ARCH.toLowerCase().contains("64")
ext.IS_MAC = OS_NAME.contains("mac") || OS_NAME.contains("darwin")
ext.IS_WINDOWS = OS_NAME.contains("windows")
ext.IS_LINUX = OS_NAME.contains("linux")
ext.OS_FAMILY = IS_MAC ? 'mac' : IS_WINDOWS ? 'windows' : IS_LINUX ? 'linux' : 'unsupported'
ext.JDK_HOME = System.getProperty("java.home")
ext.OPENJFX_VERSION = "14.0.1"
ext.OPENJFX_PLATEFORM_CLASSIFIER = IS_MAC ? 'mac' : IS_WINDOWS ? 'win' : IS_LINUX ? 'linux' : 'unsupported'
ext.OPENJFX_JMODS_DOWNLOAD_URL = "https://gluonhq.com/download/javafx-${OPENJFX_VERSION.replace(".", "-")}-jmods-${OS_FAMILY}"

ext.JLINK_ADD_MODULES = "javafx.controls," +
        "javafx.fxml," +
        "javafx.swing," +
        "java.base," +
        "java.prefs," +
        "java.sql," +
        "java.xml," +
        "java.management," +
        "java.rmi," +
        "jdk.localedata," +
        "java.management.rmi," +
        "jdk.management.agent," +
        "jdk.security.auth,jdk.management," +
        "jdk.management.jfr," +
        "jdk.zipfs," +
        "jdk.unsupported.desktop," +
        "jdk.crypto.ec," +
        "jdk.crypto.cryptoki" +
        "${IS_WINDOWS ? ",jdk.crypto.mscapi" : ""}"

ext.DISTRIBUTION_NAME = "${project.name}-${BINJR_VERSION}_${OS_FAMILY}-${OS_ARCH}"
ext.DISTRIBUTION_ROOT = "${buildDir}/distributions/${DISTRIBUTION_NAME}"
ext.DISTRIBUTION_PATH = IS_WINDOWS ? "${DISTRIBUTION_ROOT}" : "${DISTRIBUTION_ROOT}/${BINJR_VERSION}"
ext.DISTRIBUTION_EXTENSION = IS_WINDOWS ? "msi" : IS_MAC ? "dmg" : "tar.gz"
ext.WIX_HOME = System.getenv("WIX")
ext."signing.keyId" = System.getenv('GPG_KEY_NAME')
ext."signing.secretKeyRingFile" = "${projectDir}/distribution/publishing/keyring.gpg"
ext."signing.password" = System.getenv('GPG_PASSPHRASE')

System.out.println("Building version: ${BINJR_VERSION}")
System.out.println("Is tag: ${IS_TAG}")
System.out.println("Is release: ${IS_RELEASE}")


if (IS_TAG && System.getenv('REPO_TAG_NAME') != "v${BINJR_VERSION}") {
    throw new GradleException("Tag name [${System.getenv('REPO_TAG_NAME')}] does not match version [${BINJR_VERSION}]")
}

configure(subprojects) {
    if (project == project(':binjr-app'))
        apply plugin: 'application'
    else
        apply plugin: 'java-library'
    apply plugin: "de.marcphilipp.nexus-publish"
    apply plugin: "signing"
    group GROUP_ID
    version BINJR_VERSION
    sourceCompatibility = 11
    targetCompatibility = 11
    description 'Flexible Time Series Visualization'
    compileJava.options.encoding = 'UTF-8'

    if (project != project(':binjr-app')) {
        project.javadoc {
            options.encoding = 'UTF-8'
            options.tags = ["XmlJavaTypeAdapter", "XmlJavaTypeAdapters"]
        }

        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        dependencies {
            compileOnly "org.openjfx:javafx-base:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-graphics:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-controls:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-fxml:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
            compileOnly "org.openjfx:javafx-swing:$OPENJFX_VERSION:$OPENJFX_PLATEFORM_CLASSIFIER"
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                if (project != project(':binjr-app')) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
                pom {
                    name = project.name
                    description = 'A Time Series Data Browser'
                    url = 'https://binjr.eu'
                    if (project == project(':binjr-app')) {
                        packaging = 'pom'
                    } else {
                        packaging = 'jar'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'fthevenet'
                            name = 'Frederic Thevenet'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/binjr/binjr.git'
                        developerConnection = 'scm:git:https://github.com/binjr/binjr.git'
                        url = 'https://github.com/binjr/binjr'
                    }
                    if (project == project(':binjr-app')) {
                        withXml {
                            Node pluginNode = asNode().appendNode('build')
                                    .appendNode('plugins')
                                    .appendNode('plugin')
                            pluginNode.appendNode('groupId', 'org.codehaus.mojo')
                            pluginNode.appendNode('artifactId', 'exec-maven-plugin')
                            pluginNode.appendNode('version', '1.2.1')
                            pluginNode.appendNode('executions')
                                    .appendNode('execution')
                                    .appendNode('goals')
                                    .appendNode('goal', 'java')
                            pluginNode.appendNode('configuration')
                                    .appendNode('mainClass', 'eu.binjr.core.Bootstrap')

                            Node depsNode = asNode().appendNode('dependencies')
                            project.configurations.runtimeClasspath.each {
                                if (it.name.startsWith('binjr')) {
                                    Node depNode = depsNode.appendNode('dependency')
                                    depNode.appendNode('groupId', GROUP_ID)
                                    depNode.appendNode('artifactId', it.name.replace("-${BINJR_VERSION}.jar", ""))
                                    depNode.appendNode('version', BINJR_VERSION)
                                }
                            }
                        }
                    }
                }
            }
        }
    }

    signing {
        required { IS_RELEASE && gradle.taskGraph.hasTask("publishArtifacts") }
        sign publishing.publications.mavenJava
    }

    tasks.withType(Sign) {
        onlyIf { IS_RELEASE }
    }

    nexusPublishing {
        repositories {
            sonatype {
                username = System.getenv('OSSRH_JIRA_USERNAME')
                password = System.getenv('OSSRH_JIRA_PASSWORD')
                connectTimeout = Duration.ofSeconds(300)
                clientTimeout = Duration.ofSeconds(300)
            }
        }
    }
}

task publishArtifacts(dependsOn: subprojects.publishToSonatype) {
    doLast {
        System.out.println("Done publishing artifacts from subprojects.")
    }
}

task copyAppLibs(type: Copy) {
    into "$DISTRIBUTION_PATH/libs"
    from(project(":binjr-core").configurations.runtimeClasspath) {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jrds").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-csv").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-rrd4j").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-netdata").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-core").jar
}

task copyPluginLibs(type: Copy) {
    into IS_MAC ? "$DISTRIBUTION_PATH/libs" : "$DISTRIBUTION_PATH/plugins"
    from project(":binjr-adapter-jrds").jar
    from project(":binjr-adapter-csv").jar
    from project(":binjr-adapter-rrd4j").jar
    from project(":binjr-adapter-netdata").jar
}

task copyJpackageInputLibs(type: Copy) {
    into "${buildDir}/tmp/input"
    from(project(":binjr-core").configurations.runtimeClasspath) {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-jrds").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-csv").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-rrd4j").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-adapter-netdata").configurations.runtimeClasspath {
        exclude "javafx-*.jar"
    }
    from project(":binjr-core").jar
    from project(":binjr-adapter-jrds").jar
    from project(":binjr-adapter-csv").jar
    from project(":binjr-adapter-rrd4j").jar
    from project(":binjr-adapter-netdata").jar
}

task copyPlatformSpecific(type: Copy) {
    into DISTRIBUTION_PATH
    from "${projectDir}/distribution/platforms/${OS_FAMILY}"
}

task copyResources(type: Copy) {
    into "$DISTRIBUTION_PATH/resources"
    from "${projectDir}/distribution/resources"
}

task expandMdTemplates(type: Copy) {
    from "${projectDir}/distribution/templates"
    into "${buildDir}/tmp/expanded"
    filteringCharset = 'UTF-8'
    def changeHistory = new File("./CHANGELOG.md").getText("UTF-8")
    def currentChanges = new File("./UNRELEASED.md").getText("UTF-8")
    expand(["version"       : "$BINJR_VERSION",
            "releaseDate"   : "${new SimpleDateFormat("EEE, d MMM yyyy").format(new Date())}",
            "changeHistory" : "$changeHistory",
            "currentChanges": "$currentChanges",
            "tagName"       : "v${BINJR_VERSION}"
    ])
}

task copyInfo(type: Copy, dependsOn: expandMdTemplates) {
    from {
        ["${buildDir}/tmp/expanded/CHANGELOG.md",
         "${buildDir}/tmp/expanded/LICENSE.md",
         "${buildDir}/tmp/expanded/NOTICE.md",
         "${buildDir}/tmp/expanded/README.md"]
    }
    into "${buildDir}/tmp/info"
}

markdownToHtml.dependsOn(copyInfo)

markdownToHtml {
    sourceDir = file("${buildDir}/tmp/info")
    outputDir = file("$DISTRIBUTION_PATH")
    doLast {
        ConfigurableFileTree tree = fileTree(dir: "$DISTRIBUTION_PATH")
        tree.include '**/*.html'
        tree.each { File file ->
            String path = file.path
            File newFile = new File("${DISTRIBUTION_PATH}/tmp_${file.name}")
            newFile << "<html lang=\"en\">\n" +
                    "<head>\n" +
                    "    <title>" + (file.name - ".html") + "</title>\n" +
                    "    <meta charset=\"utf-8\"/>\n" +
                    "    <link rel=\"stylesheet\" href=\"./resources/css/plain.css\">\n" +
                    "</head>"
            newFile << file.getText("utf-8")
            if (!file.name.startsWith("LICENSE")) {
                newFile << "<footer>\n" +
                        "    <br>\n" +
                        "    <hr>\n" +
                        "    <p style=\"text-align:left;\">\n" +
                        "        (c) 2016-2020 <a href=\"https://www.fthevenet.eu\">Frederic Thevenet</a>\n" +
                        "        <span style=\"float:right;\"><a href=\"https://binjr.eu\">https://binjr.eu</a></span>\n" +
                        "    </p>\n" +
                        "</footer>"
            }
            file.delete()
            newFile.renameTo(path)
        }
    }
}

task downloadopenJfxJmods(type: Download) {
    src OPENJFX_JMODS_DOWNLOAD_URL
    dest new File("${buildDir}/tmp", 'openjfx-jmods.zip')
}

task downloadAndUnzipFile(dependsOn: downloadopenJfxJmods, type: Copy) {
    from zipTree(downloadopenJfxJmods.dest)
    into buildDir
}

task createRuntimeImage(type: Exec, dependsOn: [downloadAndUnzipFile]) {
    workingDir project.projectDir
    commandLine = [
            "${JDK_HOME}/bin/jlink",
            '-p', "${JDK_HOME}/jmods${File.pathSeparator}${buildDir}/javafx-jmods-${OPENJFX_VERSION}",
            '--add-modules', JLINK_ADD_MODULES,
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            "--vm=server",
            "--compress=2",
            '--output', "${DISTRIBUTION_PATH}/runtime"
    ]
    doLast {
        System.out.println("Application '${project.name}' packaged.")
        System.out.println(" -> location: ${DISTRIBUTION_PATH}/")
    }
}

task prepareAppBundle(dependsOn: [copyAppLibs, copyPluginLibs, copyResources, copyPlatformSpecific, markdownToHtml, createRuntimeImage]) {
}

task listPackageContent(type: Exec, dependsOn: prepareAppBundle) {
    workingDir DISTRIBUTION_PATH
    commandLine = [
            "sh", "-c",
            "find . -mindepth 1 | sed -n 's@^\\./@@p' > .installed"
    ]
}

task packageDistributionZip(type: Zip, dependsOn: prepareAppBundle) {
    doFirst{
        Files.copy(Paths.get("${projectDir}/distribution/bundlers/win_zip/binjr.ini"),
                Paths.get("${DISTRIBUTION_PATH}/binjr.ini"),
                StandardCopyOption.REPLACE_EXISTING)
    }
    from DISTRIBUTION_ROOT
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.zip"
}

task packageDistributionTar(type: Exec, dependsOn: [listPackageContent]) {
    workingDir DISTRIBUTION_ROOT
    commandLine = [
            "sh", "-c",
            "ln -s \"${BINJR_VERSION}/binjr\" \"binjr\" && tar czf \"${buildDir}/${DISTRIBUTION_NAME}.tar.gz\" \"${BINJR_VERSION}\" \"binjr\""
    ]
}

task wixRunHeat(type: Exec, dependsOn: prepareAppBundle) {
    doFirst{
        Files.copy(Paths.get("${projectDir}/distribution/bundlers/win_msi/binjr.ini"),
                Paths.get("${DISTRIBUTION_PATH}/binjr.ini"),
                StandardCopyOption.REPLACE_EXISTING)
    }
    workingDir project.projectDir
    commandLine = [
            "${WIX_HOME}/bin/heat.exe",
            "dir", "${DISTRIBUTION_PATH}",
            "-cg", "binjrFiles",
            "-suid", "-gg", "-scom", "-sreg", "-sfrag", "-srd",
            "-dr", "APPLICATIONFOLDER",
            "-var", "var.buildSourceDir",
            "-out", "${buildDir}/wix/binjrFiles.wxs"
    ]
}

task wixRunCandle(type: Exec, dependsOn: [wixRunHeat]) {
    workingDir project.projectDir
    commandLine = [
            "${WIX_HOME}/bin/candle.exe",
            "-arch", "x64",
            "-out", "${buildDir}/wix/obj/",
            "${projectDir}/distribution/bundlers/win_msi/binjr.wxs",
            "${buildDir}/wix/binjrFiles.wxs",
            "-dbinjrVersion=${BINJR_VERSION_STEM}",
            "-dbuildSourceDir=${DISTRIBUTION_PATH}",
            "-dresourcesDir=${projectDir}/distribution"
    ]
}

task wixRunLight(type: Exec, dependsOn: [wixRunCandle]) {
    workingDir project.projectDir
    commandLine = [
            "${WIX_HOME}/bin/light.exe",
            "-sw", "-sice:ICE38", "-sice:ICE64",
            "-out", "${buildDir}/${DISTRIBUTION_NAME}.msi",
            "${buildDir}/wix/obj/*.wixobj",
            "-ext", "WixUtilExtension", "-ext", "WixUIExtension.dll"
    ]
}

task jpackageMacOS(type: Exec, dependsOn: [prepareAppBundle, copyJpackageInputLibs]) {
    workingDir buildDir
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${buildDir}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Binjr",
            "--runtime-image", "${DISTRIBUTION_PATH}/runtime",
            "--type", DISTRIBUTION_EXTENSION,
            "--copyright", "Copyright (c) 2016-2020 Frederic Thevenet",
            "--description", "A Time Series Browser",
            "--app-version", BINJR_VERSION,
            "--icon", "${DISTRIBUTION_PATH}/resources/icons/binjr.icns",
            "--file-associations", "${projectDir}/distribution/bundlers/mac_tar/file-associations.properties",
            "--arguments", "--packaging=MAC_DMG",
            "--java-options", "-Xmx4096M",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20"
    ]
    doLast {
        def source = java.nio.file.Path.of("${buildDir}/${project.name}-${BINJR_VERSION}.${DISTRIBUTION_EXTENSION}")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.${DISTRIBUTION_EXTENSION}"))
    }
}


task jpackageDeb(type: Exec, dependsOn: [prepareAppBundle, copyJpackageInputLibs]) {
    workingDir buildDir
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${buildDir}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Binjr",
            "--runtime-image", "${DISTRIBUTION_PATH}/runtime",
            "--type", "deb",
            "--copyright", "2016-2020 Frederic Thevenet",
            "--description", "A Time Series Browser",
            "--app-version", BINJR_VERSION,
            "--icon", "${DISTRIBUTION_PATH}/resources/icons/binjr.png",
            "--license-file", "${projectDir}/LICENSE.md",
            "--file-associations", "${projectDir}/distribution/bundlers/linux_deb/file-associations.properties",
            "--resource-dir", "${projectDir}/distribution/bundlers/linux_deb/",
            "--java-options", "-Xmx4096M",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--arguments", "--packaging=LINUX_DEB",
            "--linux-package-name", "binjr",
            "--linux-deb-maintainer","binjr@free.fr",
            "--linux-app-category", "Utility",
            "--linux-menu-group", "Utility",
            "--linux-rpm-license-type", "Apache-2.0",
            "--linux-shortcut",
            "--temp", "${buildDir}/tmp/apt",
            "--verbose"
    ]
    doLast {
        def source = java.nio.file.Path.of("${buildDir}/${project.name}_${BINJR_VERSION}-1_${OS_ARCH}.deb")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.deb"))
    }
}

task jpackageRpm(type: Exec, dependsOn: [prepareAppBundle, copyJpackageInputLibs]) {
    workingDir buildDir
    commandLine = [
            "jpackage",
            "--name", project.name,
            "--input", "${buildDir}/tmp/input",
            "--main-jar", "binjr-core-${BINJR_VERSION}.jar",
            "--main-class", "eu.binjr.core.Binjr",
            "--runtime-image", "${DISTRIBUTION_PATH}/runtime",
            "--type", "rpm",
            "--copyright", "2016-2020 Frederic Thevenet",
            "--description", "A Time Series Browser",
            "--app-version", BINJR_VERSION.replace("-", "."),
            "--icon", "${DISTRIBUTION_PATH}/resources/icons/binjr.png",
            "--license-file", "${projectDir}/LICENSE.md",
            "--file-associations", "${projectDir}/distribution/bundlers/linux_rpm/file-associations.properties",
            "--resource-dir", "${projectDir}/distribution/bundlers/linux_rpm/",
            "--java-options", "-Xmx4096M",
            "--java-options", "-XX:+UnlockExperimentalVMOptions",
            "--java-options", "-XX:+UseShenandoahGC",
            "--java-options", "-XX:ShenandoahGCHeuristics=compact",
            "--java-options", "-XX:ShenandoahAllocationThreshold=20",
            "--arguments", "--packaging=LINUX_RPM",
            "--linux-package-name", "binjr",
            "--linux-app-category", "Utility",
            "--linux-menu-group", "Utility",
            "--linux-rpm-license-type", "Apache-2.0",
            "--linux-shortcut",
            "--temp", "${buildDir}/tmp/rpm",
            "--verbose"
    ]
    doLast {
        def source = java.nio.file.Path.of("${buildDir}/${project.name}-${BINJR_VERSION.replace("-", ".")}-1.x86_64.rpm")
        Files.move(source, source.resolveSibling("${DISTRIBUTION_NAME}.rpm"))
    }
}

task packageAsZip(type: WrapTaskForSigning, dependsOn: [packageDistributionZip]) {
    from "${buildDir}${DISTRIBUTION_NAME}.zip"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.zip"
}

task packageAsMsi(type: WrapTaskForSigning, dependsOn: [wixRunLight]) {
    from "${buildDir}${DISTRIBUTION_NAME}.msi"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.msi"
}

task packageAsDmg(type: WrapTaskForSigning, dependsOn: [jpackageMacOS]) {
    from "${buildDir}${DISTRIBUTION_NAME}.dmg"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.dmg"
}

task packageAsTarGz(type: WrapTaskForSigning, dependsOn: [packageDistributionTar]) {
    from "${buildDir}${DISTRIBUTION_NAME}.tar.gz"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.tar.gz"
}

task packageAsRpm(type: WrapTaskForSigning, dependsOn: [jpackageRpm]) {
    from "${buildDir}${DISTRIBUTION_NAME}.rpm"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.rpm"
}

task packageAsDeb(type: WrapTaskForSigning, dependsOn: [jpackageDeb]) {
    from "${buildDir}${DISTRIBUTION_NAME}.deb"
    destinationDir buildDir
    archiveName "${DISTRIBUTION_NAME}.deb"
}

signing {
    sign packageAsZip
    sign packageAsMsi
    sign packageAsDmg
    sign packageAsTarGz
    sign packageAsRpm
    sign packageAsDeb
}

task packageDistribution() {
    if (IS_WINDOWS) {
        finalizedBy(packageAsMsi, packageAsZip)
    } else if (IS_MAC) {
        finalizedBy(packageAsDmg, packageAsTarGz)
    } else {
        finalizedBy(packageAsTarGz, packageAsDeb, packageAsRpm)
    }
}

task signPackageDistribution() {
    if (IS_WINDOWS) {
        finalizedBy(signPackageAsMsi, signPackageAsZip)
    } else if (IS_MAC) {
        finalizedBy(signPackageAsDmg, signPackageAsTarGz)
    } else {
        finalizedBy(signPackageAsTarGz, signPackageAsDeb, signPackageAsRpm)
    }
}


def isNonStable = { String version ->
    // Ignore javafx early access and glassfish beta
    def regex = /(?i)^[0-9,.v-]+\-((ea)|(rc)|(b)|(m))[-\+]*[0-9.]+$/
    return (version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}


/**
 * This class is a no-op copy task whose purpose is to allow using the 
 * standard gradle signing plugin to sign any arbitrary file.
 * The idea is to piggy-back on the existing support for signing
 * of tasks extending AbstractArchiveTask; to use it, simply declare a 
 * task of that type and use the path to the file to be signed both as 
 * the source and target of the archive.
 * NB: No copy/move/archiving will actually take place.
 */
class WrapTaskForSigning extends AbstractArchiveTask {

    @Override
    protected CopyAction createCopyAction() {
        return new noopCopyAction();
    }

    class noopCopyAction implements CopyAction {

        @Override
        WorkResult execute(CopyActionProcessingStream stream) {
            return WorkResults.didWork(true);
        }
    }
}
